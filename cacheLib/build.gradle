apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def siteUrl = 'https://github.com/DovSnier/cacheKit'
def gitUrl = 'git@github.com:DovSnier/cacheKit.git'

android {

    compileSdkVersion rootProject.ext.compile_sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
        buildConfigField "String", "cache_sdk_version", "\"${rootProject.ext.version_name}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile("com.android.support.test.espresso:espresso-core:${rootProject.ext.espresso_core_version}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:support-annotations:${rootProject.ext.support_annotations_version}"
    testCompile "junit:junit:${rootProject.ext.junit_version}"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def user = properties.getProperty("bintray.user")
def key = properties.getProperty("bintray.apikey")
publish {
    repoName = 'AndroidComponent'
    userOrg = 'dovsnier'
    groupId = 'com.dvsnier'
    artifactId = 'cacheLib'
    publishVersion = rootProject.ext.version_name
    desc = "the android cache lib that version is ${rootProject.ext.version_name}"
    website = siteUrl
    repository = gitUrl
    autoPublish = true
    bintrayUser = user
    bintrayKey = key
}
